use lalrpop_util::ParseError;

use super::syntax;
use super::lex;
use types;
use expr;

grammar;

pub Ident: syntax::Ident = {
    <name: Name> <collision_id: ("#" <UInt>)?> => {
        syntax::Ident {
            name,
            collision_id: collision_id.unwrap_or(0),
        }
    },
};

TypeParam: syntax::TypeParam = {
    <ident: Ident> => {
        syntax::TypeParam {
            ident,
        }
    },
};

AtomicType: syntax::Type = {
    "(" <Type> ")",
    "(" ")" => syntax::Type::Unit,
    <ident: Ident> => syntax::Type::Var { ident },
};

AppType: syntax::Type = {
    <AtomicType>,
    <constructor: AppType> <param: AtomicType> => {
        syntax::Type::App {
            constructor: Box::new(constructor),
            param: Box::new(param),
        }
    },
};

Quantifier: types::Quantifier = {
    "exists" => types::Quantifier::Exists,
    "forall" => types::Quantifier::ForAll,
};

QuantifiedType: syntax::Type = {
    <AppType>,

    <quantifier: Quantifier> "{" <param: TypeParam> "}" <body: QuantifiedType> => {
        syntax::Type::Quantified {
            quantifier,
            param,
            body: Box::new(body),
        }
    },

    <arg: AtomicType> "->" <ret: QuantifiedType> => {
        syntax::Type::Func {
            arg: Box::new(arg),
            ret: Box::new(ret),
        }
    },
};

PairType: syntax::Type = {
    <QuantifiedType> ","?,
    <left: QuantifiedType> "," <right: PairType> => {
        syntax::Type::Pair {
            left: Box::new(left),
            right: Box::new(right),
        }
    },
};

pub Type: syntax::Type = {
    <PairType>,
};

AtomicExpr: syntax::Expr = {
    "(" <Expr> ")",

    "(" ")" => syntax::Expr::Unit,

    <ident: Ident> => {
        syntax::Expr::Var {
            usage: expr::VarUsage::Copy,
            ident,
        }
    },

    "move" <ident: Ident> => {
        syntax::Expr::Var {
            usage: expr::VarUsage::Move,
            ident,
        }
    },

    <callee: CallableExpr> "(" <arg: Expr> ")" => {
        syntax::Expr::App {
            callee: Box::new(callee),
            arg: Box::new(arg),
        }
    }
};

InstExpr: syntax::Expr = {
    <receiver: AtomicExpr> <type_params: ("{" <Type> "}")+> => {
        syntax::Expr::Inst {
            receiver: Box::new(receiver),
            type_params,
        }
    }
};

CallableExpr: syntax::Expr = {
    <AtomicExpr>,
    <InstExpr>,
};

BlockExpr: syntax::Expr = {
    <CallableExpr>,

    "forall" <type_params: ("{" <TypeParam> "}")+> <body: BlockExpr> => {
        syntax::Expr::ForAll {
            type_params,
            body: Box::new(body),
        }
    },

    "func" "(" <arg_name: Ident> ":" <arg_type: Type> ")" "->" <body: BlockExpr> => {
        syntax::Expr::Func {
            arg_name,
            arg_type,
            body: Box::new(body),
        }
    },

    "let" <names: (<Ident> ",")*> <last_name: Ident?> "=" <val: Expr>
    "in" <body: BlockExpr> =>? {
        let mut all_names = names;
        if let Some(last_name) = last_name {
            all_names.push(last_name);
        }
        if all_names.len() > 0 {
            Ok(syntax::Expr::Let {
                names: all_names,
                val: Box::new(val),
                body: Box::new(body),
            })
        } else {
            Err(ParseError::User { error: lex::Error::Empty })
        }
    },

    "let_exists"
    <type_names: ("{" <Ident> "}")+>
    <val_name: Ident> "=" <val: Expr>
    "in" <body: BlockExpr> => {
        syntax::Expr::LetExists {
            type_names: type_names,
            val_name,
            val: Box::new(val),
            body: Box::new(body),
        }
    },

    "make_exists"
    <params: ("{" <Ident> "=" <Type> "}")+>
    <type_body: Type>
    "of" <body: BlockExpr> => {
        syntax::Expr::MakeExists {
            params: params,
            type_body,
            body: Box::new(body),
        }
    },
};

PairExpr: syntax::Expr = {
    <BlockExpr> ","?,

    <left: BlockExpr> "," <right: PairExpr> => {
        syntax::Expr::Pair {
            left: Box::new(left),
            right: Box::new(right),
        }
    },
};

pub Expr: syntax::Expr = {
    <PairExpr>,
};

extern {
    type Location = usize;
    type Error = lex::Error;

    enum lex::Token {
        Name => lex::Token::Name(<String>),
        UInt => lex::Token::UInt(<u64>),

        "move" => lex::Token::KeyMove,
        "func" => lex::Token::KeyFunc,
        "let" => lex::Token::KeyLet,
        "let_exists" => lex::Token::KeyLetExists,
        "in" => lex::Token::KeyIn,
        "make_exists" => lex::Token::KeyMakeExists,
        "of" => lex::Token::KeyOf,

        "forall" => lex::Token::KeyForall,
        "exists" => lex::Token::KeyExists,
        "Version" => lex::Token::KeyVersion,
        "Place" => lex::Token::KeyPlace,

        "#" => lex::Token::NumSign,
        "," => lex::Token::Comma,
        ";" => lex::Token::Semicolon,
        "=" => lex::Token::Equals,
        ":" => lex::Token::Colon,
        "*" => lex::Token::Star,
        "->" => lex::Token::Arrow,

        "(" => lex::Token::OpenPar,
        ")" => lex::Token::ClosePar,

        "{" => lex::Token::OpenCurly,
        "}" => lex::Token::CloseCurly,
    }
}

// Nonterminals which are just thin wrappers over terminals, for testing the lexer itself:

pub RawName: String = {
    <Name>,
};

pub Whitespace: () = {
    () => (),
};
